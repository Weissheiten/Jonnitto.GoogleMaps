// Namespaces are defined in "Root.fusion"

prototype(Page) {
	head.googleMaps = Fusion:Tag {
		tagName = 'link'
		attributes {
			rel = 'stylesheet'
			href = Fusion:ResourceUri {
				path = 'resource://Jonnitto.GoogleMaps/Public/Main.css'
			}
		}
		@if.render = GM:RenderMap {
			include = ${Configuration.setting('Jonnitto.GoogleMaps.includeCSS')}
		}
	}

	body.javascripts {
		googleMapsFunction = Fusion:Tag {
			tagName = 'script'
			attributes.src = Fusion:ResourceUri {
				path = 'resource://Jonnitto.GoogleMaps/Public/Main.js'
			}
			@position = 'end 100'
			@if.render = GM:RenderMap {
				include = ${Configuration.setting('Jonnitto.GoogleMaps.includeFunction')}
			}
		}
		googleMapsApi = Fusion:Tag {
			tagName = 'script'
			attributes.src = ${String.replace(Configuration.setting('Jonnitto.GoogleMaps.googleAPI'), '{APIKey}', Configuration.setting('Jonnitto.GoogleMaps.APIKey'))}
			attributes.id = 'jonnitto-googlemaps-api'
			@position = 'after googleMapsFunction'
			@if.render = GM:RenderMap {
				include = ${Configuration.setting('Jonnitto.GoogleMaps.includeGoogleAPI')}
			}
		}
		googleMapsBackend = Fusion:Tag {
			tagName = 'script'
			attributes.src = Fusion:ResourceUri {
				path = 'resource://Jonnitto.GoogleMaps/Public/Backend.js'
			}
			@position = 'after googleMapsApi'
			@if.render = GM:RenderMap {
				include = ${Configuration.setting('Jonnitto.GoogleMaps.includeBackendJS')}
				live = false
			}
		}
	}
}
