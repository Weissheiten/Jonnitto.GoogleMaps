prototype(GM:Map) {
	configuration = ${Configuration.setting('Jonnitto.GoogleMaps')}
	cssClasses = ${this.configuration.cssClasses}
	APIKey = ${this.configuration.APIKey}
	mapLocation = ${String.trim(q(node).property('mapLocation'))}
	streetviewLocation = ${String.trim(q(node).property('streetviewLocation'))}

	hasMap = ${this.mapLocation ? true : false}
	hasStreetview = ${this.streetviewLocation ? true : false}

	@context {
		configuration = ${this.configuration}
		cssClasses = ${this.cssClasses}

		mapLocation = ${this.mapLocation}
		streetviewLocation = ${this.streetviewLocation}

		hasMap = ${this.hasMap}
		hasStreetview = ${this.hasStreetview}
		hasMapAndStreet = ${this.mapLocation && this.streetviewLocation ? true : false}
	}

	attributes.class = Fusion:RawArray {
		both = ${hasMapAndStreet ? cssClasses.both.wrapper : false}
	}

	mapAttributes = Fusion:Attributes {
		class = Fusion:RawArray {
			google = ${cssClasses.map}
			both = ${hasMapAndStreet ? cssClasses.both.map : false}
		}
		data-location = ${mapLocation}
		data-zoom = ${q(node).property('zoom')}
		data-showinfo = ${q(node).property('showinfo')}
	}

	streetViewAttributes = Fusion:Attributes {
		class = Fusion:RawArray {
			google = ${cssClasses.streetview}
			both = ${hasMapAndStreet ? cssClasses.both.streetview : false}
		}
		data-location = ${streetviewLocation}
		data-heading = ${q(node).property('heading')}
		data-pitch = ${q(node).property('pitch')}
	}

	title = ${q(node).property('title')}
	text = ${q(node).property('text')}
}
